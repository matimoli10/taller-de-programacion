3. Realice un programa con 2 equipos:
- El equipo A, compuesto por los robots A1 y A2, debe juntar papeles de las
primeras 20 esquinas de la calle 1
- El equipo B, compuesto por los robots B1 y B2, debe juntar flores de las primeras
20 esquinas de la calle 5
Los robots A1 y B1 deberán realizar las 10 primeras esquinas de su recorrido, y al
finalizar avisarán a los robots A2 y B2 respectivamente para que continúen con las
siguientes 10 esquinas. El segundo robot de cada equipo debe informar la cantidad
de elementos recogidos en las 20 esquinas.
Inicialice los 4 robots en las esquinas que considere más apropiadas según el
trayecto que le corresponde realizar a cada uno.



programa ejemplo
procesos
  proceso juntarpapeles (ES cant: numero)
  comenzar
    mientras(HayPapelEnLaEsquina)
      tomarPapel
      cant:= cant + 1
  fin
  proceso juntarflores(ES cant: numero)
  comenzar
    mientras (HayFlorEnLaEsquina)
      tomarFlor
      cant:= cant + 1
  fin
  proceso recorrecalle(ES cant: numero; E soy: numero)
  comenzar
    si ((soy = 1) | (soy = 3))
      repetir 9
        juntarpapeles(cant)
        mover
      juntarpapeles(cant)
    sino
      repetir 9
        juntarflores(cant)
        mover
      juntarflores(cant)
  fin
areas
  area1: AreaP (1,1,10,1)
  area2: AreaP (1,5,10,5)
  area3: AreaP (11,1,20,1)
  area4: AreaP (11,5,20,5)
  area5: AreaP (1,3,1,3)
robots
  robot ordenes
  variables
    cant:numero
    soy: numero
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
    EnviarMensaje(4,r4)
    
    repetir 2
      RecibirMensaje(soy,*)
      si(soy = 1)
        RecibirMensaje(cant,r1)
        EnviarMensaje(cant,r3)
      sino 
        RecibirMensaje(cant,r2)
        EnviarMensaje(cant,r4)
  fin
  robot tipo1
  variables
    cant : numero
    soy : numero
  comenzar
    derecha
    cant := 0
    RecibirMensaje(soy,jefe)
    recorrecalle(cant,soy)
    EnviarMensaje(soy,jefe)
    EnviarMensaje(cant,jefe)
  fin
  robot tipo2
  variables
    cant: numero
    soy: numero
    avenida: numero
    calle: numero
  comenzar
    derecha
    RecibirMensaje(soy,jefe)
    RecibirMensaje(cant,jefe)
    recorrecalle(cant,soy)
    Informar(cant)
  fin
variables
  r1: tipo1
  r2: tipo1
  r3: tipo2
  r4: tipo2
  jefe: ordenes
comenzar
  AsignarArea(r1, area1)
  AsignarArea(r2, area2)
  AsignarArea(r3, area3)
  AsignarArea(r4, area4)
  AsignarArea(jefe, area5)
  Iniciar(r1,1,1)
  Iniciar(r2,1,5)
  Iniciar(r3,11,1)
  Iniciar(r4,11,5)
  Iniciar(jefe,1,3)
fin
